import org.hidetake.gradle.swagger.generator.GenerateSwaggerUI

plugins {
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id("com.github.spotbugs") version "6.1.3"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'

    implementation 'org.cip4.lib.jdf:JDFLibJ-JSON:1.2.2'
    implementation 'org.cip4.lib.jdf:JDFLibJ:2.1.8.11'
    implementation('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude(group: "junit", module: "junit")
    }
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'

    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation 'com.networknt:json-schema-validator:1.5.5'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.4'

    swaggerUI 'org.webjars:swagger-ui:5.15.1'
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

swaggerSources {
    xjdf_22 {
        inputFile = file('build/resources/main/xjdf_2_2.yml')
    }
    xjdf_2x {
        inputFile = file('build/resources/main/xjdf_2_x.yml')
    }
}

tasks.withType(GenerateSwaggerUI).configureEach {
    dependsOn 'generateOpenApiSpec'
}

tasks.register('generateOpenApiSpec', JavaExec) {
    group = 'build'
    mainClass = 'org.cip4.xjdf.json.openapi.Main'
    classpath = sourceSets.main.runtimeClasspath
}